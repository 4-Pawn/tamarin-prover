theory STS_ENC_FIX3
begin

section{* The Station-To-Station Protocol (ENC version) *}
// Fix with flow numbers and identities

// Public key infrastructure
/**
 * The !Pk facts can be regarded as certificates
 */
rule Register_pk:
  [ Fr(~ltk) ] 
  --[ LtkSet($A, ~ltk) ]-> 
  [ !Ltk($A, ~ltk), !Pk($A, pk(~ltk)), !Pk('Eve', pk(~ltk)) ]

rule Get_pk:
  [ !Pk($A, k) ] 
  --> 
  [ Out(k) ]

rule Reveal_ltk:
    [ !Ltk($A, ~ltk) ]
  --[ LtkReveal(~ltk) ]->
    [ Out(~ltk) ]

// Protocol
rule Init_1:
  [ Fr(~ekI), !Ltk($I, ~ltkI) ]
  -->
  [ Init_1( $I, $R, ~ltkI, ~ekI )
  , Out( <$I, $R, 'g' ^ ~ekI> ) ]

rule Init_2:
    [ Init_1( $I, $R, ~ltkI, ~ekI )
    , !Pk($R, pk(~ltkR))
    , In( <$R, $I, Y, senc{
          sign{h(<'1',$I,$R, Y, 'g'^~ekI >) }~ltkR
	}(Y^~ekI)
      > ) ]
  --[ SessionKey(~ekI,$I,$R, h(Y ^ ~ekI)), 
      AcceptedI(~ekI,$I,$R,'g'^~ekI,Y) ]->
    [ Out( <$I, $R, senc{
          sign{h(<'2',$I,$R, 'g' ^ ~ekI, Y >) }~ltkI
	}(Y^~ekI)
      > ) ]

rule Resp_1:
    [ !Ltk($R, ~ltkR)
    , Fr(~ekR)
    , In( <$I, $R, X > ) ]
  -->
    [ Resp_1( $I, $R, ~ltkR, ~ekR, X )
    , Out( <$R, $I, 'g' ^ ~ekR, senc{
          sign{h(<'1',$I,$R, 'g' ^ ~ekR, X >) }~ltkR
        }(X^~ekR)
      > ) ]

rule Resp_2:
    [ !Pk($I, pk(~ltkI))
    , Resp_1( $I, $R, ~ltkR, ~ekR, X )
    , In( <$I, $R, senc{
          sign{h(<'2',$I,$R, X, 'g'^~ekR >) }~ltkI
	}(X^~ekR)
      > ) ]
  --[ SessionKey(~ekR,$I,$R, h(X ^ ~ekR) ),
      AcceptedR(~ekR,$I,$R,X,'g'^~ekR) ]->
    []

rule Sessionkey_Reveal:
    [ SessionKey(~tid, $I,$R,k) ]
  --[ SesskRev(~tid,k) ]->
    [ Out(k) ]

lemma KI_Perfect_Forward_Secrecy_I:
  "not (Ex ttest I R sessKey #i1 #i2 #k hki hkr.
     not (I = 'Eve') &
     not (R = 'Eve') &
     AcceptedI(ttest,I,R,hki,hkr) @ i1 &
     SessionKey(ttest,I,R,sessKey) @ i2 &
     K(sessKey) @ k
     &
     // No session key reveal of test
     not (Ex #i3. SesskRev(ttest, sessKey) @ i3)
     &
     // No session key reveal of partner
     not (Ex #i3 #i4 tpartner kpartner. SesskRev(tpartner, kpartner) @ i3
       & AcceptedR(tpartner,I,R,hki,hkr) @ i4
     )
     &
     not (Ex #r #s ltk. LtkSet(I,ltk) @ s & LtkReveal(ltk) @ r & r < i1)
     & 
     not (Ex #r #s ltk. LtkSet(R,ltk) @ s & LtkReveal(ltk) @ r & r < i1)
     )
  "

lemma KI_Perfect_Forward_Secrecy_R:
  "not (Ex ttest I R sessKey #i1 #i2 #k hki hkr.
     //not (I = 'Eve') &
     //not (R = 'Eve') &
     AcceptedR(ttest,I,R,hki,hkr) @ i1 &
     SessionKey(ttest,I,R,sessKey) @ i2 &
     K(sessKey) @ k
     &
     // No session key reveal of test
     not (Ex #i3. SesskRev(ttest, sessKey) @ i3)
     &
     // No session key reveal of partner
     not (Ex #i3 #i4 tpartner kpartner. SesskRev(tpartner, kpartner) @ i3
       & AcceptedI(tpartner,I,R,hki,hkr) @ i4
     )
     &
     not (Ex #r #s ltk. LtkSet(I,ltk) @ s & LtkReveal(ltk) @ r & r < i1)
     & 
     not (Ex #r #s ltk. LtkSet(R,ltk) @ s & LtkReveal(ltk) @ r & r < i1)
     )
  "



end
