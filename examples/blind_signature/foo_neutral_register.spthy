theory Blind_Signature
begin

/*
 * Protocol: Foo	
 * Modeler: 	
 * Date: 	
 * Source:	
 *
 * Status: Work in progress, proven not secure
 */

functions: blind/2, sign/2, unblind/2, open/2, commit/2, checksign/2, pk/1

equations: open(commit(m,r),r)=m, checksign( sign(m,sk),pk(sk) ) = m, unblind(sign(blind(m,r),sk),r)=sign(m,sk)

// Public key infrastructure
rule Register_pk:
  [ Fr(~ltkV) ] --[ ]-> [ !Ltk($V, ~ltkV), !Pk($V, pk(~ltkV)), Out(pk(~ltkV)) ]

rule Reveal_ltk:
  [ !Ltk(A, ltkA) ] --[ RevLtk(A) ]-> [ Out(ltkA) ]


/* We formalize the following protocol

  protocol FOO {
    Phase 1:
    1. V -> A: sign(blind(commit(v,r),b),skV)
    2. A -> V: sign(blind(commit(v,r),b),skA)
    Phase 2:
    3. V -> C: <l, sign(commit(v,r),skA)>
    Phase 3
    5. V -> C: <l, r> 
    6. C -> Pub: v
  }
*/

//Rules

rule V_1:
  let m1 = sign (blind( commit(~vote, ~r), ~b), ltkV)
  in
    [ Fr( ~r ), Fr( ~b ), Fr( ~vote ), !Ltk( $V, ltkV ) ]
  --[ OUT_V(m1)]->
    [ Out( m1 ), St_V_1( $V, ~vote, ~r, ~b ) ]

rule A_1:
  let rm = sign (blind( commit(vote, r), b), ltkV)
      sm = sign (blind( commit(vote, r), b), ltkA )
  in
    [ In( rm ), !Ltk( $A, ltkA ), !Pk( $V, pk(ltkV) ) ]
  --[ Signature (vote), IN_A_v(vote, rm), OUT_A(sm) ]->
    [ Out( sm ) ]

rule V_2:
  let m = blind( commit(vote, r), b)
  in
    [ In( sign( m, ltkA ) ), St_V_1( V, vote, r, b), Fr( ~l ), !Pk( $C, pkC ), !Pk( $A, pk(ltkA) ) ]
  --[ ]->
    [ P_Ch_1( V, ~l, sign( commit( vote, r ), ltkA ) ), St_V_2( V, $A, vote, r, b, ~l ) ]

rule C_1:
  let m = sign(commit(vote,r),ltkA)
  in
    [ P_Ch_1( V, l, m ), !Pk( A, pk(ltkA) ), !Ltk( $C, skC )]
  --[ ]->
    [ St_C_1($C, V, A, l, m ) ]

rule V_3:
    [ St_V_2( V, A, vote, r, b, l ), !Pk( $C, pkC ) ]
  --[ ]->
    [ P_Ch_2( l, r) ]

rule C_2:
  let m = sign(commit(vote,r),ltkA)
  in
    [ P_Ch_2( l, r), !Ltk( $C, skC ), St_C_1($C, $V, A, l, m ),!Pk( A, pk(ltkA) ) ]
  --[ Voted (vote) ]->
    [ Out( vote ) ]


// Avoid cycling with A_1
lemma types [typing]:
  " (All v m #i.
       IN_A_v( v, m) @ i
       ==>
       ( (Ex #j V. RevLtk(V) @ j)
       | (Ex #j. OUT_V( m) @ j)
       )
    )"


lemma elligibility:
  " 
  All vote #j.
  Voted(vote) @ j
  ==> 
  ( ( Ex #i.  Signature(vote) @ i & #i < #j )  | (Ex #k A. RevLtk( A ) @ k ) )"

lemma test1:
  exists-trace
  " Ex k #i #j. Voted (k) @ #i & Signature(k) @ j "






end

