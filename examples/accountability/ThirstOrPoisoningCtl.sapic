/*
 * Protocol:    Causality - Dessert traveller protocol {with control}
 * Modeler:     Robert KÃ¼nnemann, Ilkan Esiyok
 * Date:        Feb 2018
 * Source:
 * */

theory ThirstOrPoisoningControl
begin

builtins: signing

functions: 
  extract/1, 
  sk/1[private]

/* options: progress */

//Poisoned
let A = 0

//Shot
let B = 0

//Victim
let C = 
  let sig_p = sign(sk('A'), ~p)
      sig_s = sign(sk('B'), ~s)
  in
    (   in('c',sig_p); 
        ( ( event DrinkAll();  
             (event Poisoned();event Control('0','1'); event Verdict()) 
            +(in('c',sig_s); event CanteenShoot(); event Control('0','2');event Verdict()))
         +(in('c',sig_s); event CanteenShoot(); event Control('0','3');event Verdict())))
    +
    (   in('c',sig_s); event CanteenShoot(); event Control('0','4');event Verdict())
    +
    (   event DrinkAll(); event Control('0','5');event Healthy(); event Verdict()) 
        
    
new ~p; new ~s;
(A || B || C ||
 !(in ('c',<'corrupt',$x>); event Corrupted($x); 
     !(
       (if $x='A' then out('c',sign(sk('A'), ~p)))
     ||
       (if $x='B' then out('c',sign(sk('B'), ~s)))
     )
   )
)

lemma sanity[reuse]:
	"All #i. Verdict()@i 
      ==> ((Ex #j. Poisoned()@j) | (Ex #j. CanteenShoot()@j) | (Ex #j. Healthy()@j)) "


verdictfunction v1:
    "Ex #i #j #k. Verdict()@k & Poisoned()@i & Control('0','1')@j " -> <'A'>,
    "Ex #i #j #k. Verdict()@k & CanteenShoot()@i & Control('0','2')@j " -> <'B'>,
    "Ex #i #j #k. Verdict()@k & CanteenShoot()@i & Control('0','3')@j " -> <'B'>,
    "Ex #i #j #k. Verdict()@k & CanteenShoot()@i & Control('0','4')@j " -> <'B'>,
    otherwise -> empty

lemma acc:
    v1 accounts [control]
    for "not((Ex #j. Verdict()@j) & ( (Ex #j. Poisoned()@j) | (Ex #j. CanteenShoot()@j)))"
    for parties <'A','B'>

end


// summary of summaries:

// analyzed: ThirstOrPoisoningCtl.spthy

//   sanity (all-traces): verified (20 steps)
//   acc_excl_0_1 (all-traces): verified (81 steps)
//   acc_excl_0_2 (all-traces): verified (81 steps)
//   acc_excl_0_3 (all-traces): verified (81 steps)
//   acc_excl_0_4 (all-traces): verified (2 steps)
//   acc_excl_1_2 (all-traces): verified (160 steps)
//   acc_excl_1_3 (all-traces): verified (160 steps)
//   acc_excl_1_4 (all-traces): verified (2 steps)
//   acc_excl_2_3 (all-traces): verified (160 steps)
//   acc_excl_2_4 (all-traces): verified (2 steps)
//   acc_excl_3_4 (all-traces): verified (2 steps)
//   acc_exh (all-traces): verified (6 steps)
//   acc_suf_0 (exists-trace): verified (6 steps)
//   acc_suf_1 (exists-trace): verified (6 steps)
//   acc_suf_2 (exists-trace): verified (6 steps)
//   acc_suf_3 (exists-trace): verified (6 steps)
//   acc_compl_empty_4 (all-traces): verified (85 steps)
//   acc_compl_nonempty_0 (all-traces): verified (4 steps)
//   acc_compl_nonempty_1 (all-traces): verified (4 steps)
//   acc_compl_nonempty_2 (all-traces): verified (4 steps)
//   acc_compl_nonempty_3 (all-traces): verified (4 steps)
//   acc_min_0_0 (all-traces): verified (33 steps)
//   acc_min_1_0 (all-traces): verified (72 steps)
//   acc_min_2_0 (all-traces): verified (72 steps)
//   acc_min_3_0 (all-traces): verified (72 steps)
//   acc_uniq_0 (all-traces): verified (33 steps)
//   acc_uniq_1 (all-traces): verified (101 steps)
//   acc_uniq_2 (all-traces): verified (101 steps)
//   acc_uniq_3 (all-traces): verified (94 steps)
//   acc_rel_0_0 (all-traces): verified (1519 steps)
//   acc_rel_1_1 (all-traces): verified (6255 steps)
//   acc_rel_2_2 (all-traces): verified (6255 steps)
//   acc_rel_3_3 (all-traces): verified (3195 steps)
//   acc_unrel_0_1 (all-traces): verified (1 steps)
//   acc_unrel_0_2 (all-traces): verified (1 steps)
//   acc_unrel_0_3 (all-traces): verified (1 steps)
//   acc_unrel_1_2 (all-traces): verified (1 steps)
//   acc_unrel_1_3 (all-traces): verified (1 steps)
//   acc_unrel_2_3 (all-traces): verified (1 steps)

// ==============================================================================
// 127851.86user 27904.49system 48:39.23elapsed 5335%CPU (0avgtext+0avgdata 1925100maxresident)k
// 0inputs+0outputs (0major+507419minor)pagefaults 0swaps






