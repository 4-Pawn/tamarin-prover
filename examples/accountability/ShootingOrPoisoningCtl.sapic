/*
 * Protocol:    Causality - Late Preemption protocol
 * Modeler:     Robert KÃ¼nnemann, Ilkan Esiyok
 * Date:        Feb 2018
 * Source:
 * */
theory ShootingOrPoisoningControl
begin

builtins: signing

functions: 
  extract/1, 
  sk/1[private]

//Poisoned
let A = 0

//Shot
let B = 0

//Victim
let C = 
  let sig_p = sign(sk('A'), ~p)
      sig_s = sign(sk('B'), ~s)
  in
    (   in(sig_p); event DrinkAll();  
        event Control('0','1');event Poisoned(); event Verdict())
    +
    (   in(sig_p); event DrinkAll(); in(sig_s); 
        event Control('0','2');event Shoot(); event Verdict())
    +
    (   in(sig_s); event Control('0','3');event Shoot(); event Verdict())
    +
    (   event DrinkAll(); event Control('0','4');event Healthy(); event Verdict()) 
        
    
new ~p; new ~s;
(A || B || C ||
  ( out(pk(sk('A')));
    out(pk(sk('B')))
  )
||
 !(in ('c',<'corrupt',$x>); event Corrupted($x); 
     !(
       (if $x='A' then out(sign(sk('A'), ~p)))
     ||
       (if $x='B' then out(sign(sk('B'), ~s)))
     )
   )
)

lemma sanity[reuse]:
	"All #i. Verdict()@i 
      ==> ((Ex #j. Poisoned()@j) | (Ex #j. Shoot()@j) | (Ex #j. Healthy()@j)) "


verdictfunction v1:
    "Ex #i. Poisoned()@i " -> <'A'>,
    "Ex #i. Shoot()@i " -> <'B'>,
    otherwise -> empty

lemma acc:
    v1 accounts [control]
    for "not((Ex #j. Poisoned()@j) | (Ex #j. Shoot()@j))"
    for parties <'A','B'>

end
